#import "@ligo/fa/lib/fa2/asset/single_asset.mligo" "FA2"
#import "./errors.jsligo" "Errors"

export type t = address;
export type fa2TransferContract = contract<FA2.transfer>;

const getTransferEntrypoint = (addr: address) : fa2TransferContract => {
    const transferOption: option<fa2TransferContract> = Tezos.get_entrypoint_opt("%transfer", addr);
    return match(transferOption, {
        None: () => failwith(Errors.receiverNotFound),
        Some: (c: fa2TransferContract) => c
    });
};

const transfer = ([token_addr, from_, to_, amount_]: [t, address, address, nat]) : operation => {
    const dest = getTransferEntrypoint(token_addr);
    const transfer_requests : FA2.transfer = list([
      {from_: from_, txs: list([{to_: to_, amount: amount_, token_id: 0 as nat}])}
    ]);

    return Tezos.transaction(transfer_requests, (0 as tez), dest)
};

const getTotalSupply = (token_addr: t) : option<nat> =>
    Tezos.call_view("total_supply", unit, token_addr)
