#import "./helpers/token.jsligo" "Token_helper"
#import "./helpers/dao.jsligo" "DAO_helper"
#import "./helpers/log.jsligo" "Log"
#import "./helpers/assert.jsligo" "Assert"
#import "./bootstrap/bootstrap.jsligo" "Bootstrap"
#import "../src/main.jsligo" "DAO"

const _ = Log.describe("[Lock] test suite");

// Boostrapping of the test environment
const initTokAmount = 10 as nat;
const bootstrap = (initDAOStorage : DAO.storage) : [
    Token_helper.originated,
    DAO_helper.originated,
    address] => Bootstrap.boot(initTokAmount, initDAOStorage);
const baseConfig = DAO_helper.baseConfig;
const baseStorage = DAO_helper.baseStorage;

// Succesful lock
const testSuccess = (() : unit => {
    const [tok, dao, sender_] = bootstrap(baseStorage);

    const amount_ = 3 as nat;
    const r = DAO_helper.lock(amount_, dao.contr);
    Assert.txSuccess(r);

    DAO_helper.assertLocked(dao.taddr, sender_, amount_);
    Token_helper.assertBalanceAmount(
        tok.taddr,
        sender_,
        abs(initTokAmount - amount_));
})();

// Succesful lock before voting starts
const testSuccessBeforeVotingStarts = (() : unit => {
    // Add suffiscient start_delay
    const config = { ...baseConfig, startDelay: 86400 as nat };
    let daoStorage = { ...baseStorage, config: config };
    const [tok, dao, sender_] = bootstrap(daoStorage);
    daoStorage = Test.get_storage (dao.taddr);

    const amount_ = 3 as nat;
    DAO_helper.proposeSuccess(DAO_helper.dummyProposal, dao.contr);
    DAO_helper.lockSuccess(amount_, dao.contr);

    DAO_helper.assertLocked(dao.taddr, sender_, amount_);
    Token_helper.assertBalanceAmount(
        tok.taddr,
        sender_,
        abs(initTokAmount - amount_ - daoStorage.config.depositAmount)
    );
})();

// Failing lock because sender has insuffiscient balance
const testFailureInsBalance = (() : unit => {
    const [_, dao, _] = bootstrap(baseStorage);
    const r = DAO_helper.lock(30 as nat, dao.contr);
    Token_helper.assertInsBalanceFailure(r);
})();

// Failing lock because voting is underway
const testFailureVotingPeriod = (() : unit => {
    // Really short start_delay
    const config = { ...baseConfig, startDelay: 10 as nat };
    const daoStorage = { ...baseStorage, config: config };
    const [_, dao, _] = bootstrap(daoStorage);

    DAO_helper.proposeSuccess(DAO_helper.dummyProposal, dao.contr);
    const r = DAO_helper.lock(3 as nat, dao.contr);

    Assert.stringFailure(r, DAO.Errors.votingPeriod);
})();
